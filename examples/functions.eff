; Effectus example:
; Using local functions (FUNC) with
; some examples using inline machine
; code mnemonics

BYTE i,j
CARD z

CARD FUNC Sum(CARD nn1,nn2)
CARD result

result=nn1+nn2

RETURN(result)

CARD FUNC MultiplyB=*(BYTE a,x)[
 $85 $E0 $86 $E1 $A9 $00 $85 $A0 $A2
 $08 $46 $E0 $90 $03 $18 $65 $E1 $6A
 $66 $A0 $CA $D0 $F3 $85 $A1
 $60]

BYTE FUNC PokeTest=*(BYTE d)[
  $8D $02C6
  $60
]

PROC Main()
CARD n1, n2, n3

Put(125)
PrintE("Effectus example:")
PrintE("Using local functions (FUNC)")
PutE()

n1=30
n2=120
PrintE("Custom function Sum")
PutE()
Print("n1=30, n2=120, with sum of n3=")
n3=Sum(n1,n2)
PrintCE(n3)

n1=255
n3=Sum(n1,255)
Print("n1=255 with 255 added is ")
PrintCE(n3)

PutE()

PrintE("Custom machine language functions:")
PrintE("MultiplyB, PokeTest")

; Poke background with color         
PokeTest(210)
        
; Multiply two numbers        
i=20
j=30
z=MultiplyB(i,30)

PrintF("%EResult of assembler multiplication routine with parameters 20, 30 is %U%E%E",z)
PrintE("-----------------------------------")

PutE()
PrintE("Press trigger")
WHILE Strig(0)=1 DO OD
PrintE("Trigger pressed")
PrintE("Program finished!")

RETURN
