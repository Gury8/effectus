; Effectus example
; ----------------
; Player/Missile graphics demo

PROC MAIN()

; REGISTER VARIABLES

CHAR key
BYTE PMBASE=$D407,
     SDMCTL=$22F,
     GRACTL=$D01D,
     SIZEP0=$D008,
     PCOLR0=$2C0,
     HPOSP0=$D000,
     RAMTOP=$6A

; PLAYER 0 BITMAP
BYTE ARRAY P0=[0 0 28 28 8 28 58 89 24 40 76 68 68 0 0]

; P/M memory storage
CARD PGMMEM

; TURN OFF P/M GRAPHICS
GRACTL=0

; GRAPHICS MODE 0
GRAPHICS(0)

; SET SAFE PLACE FOR OUR PLAYER
PGMMEM=RAMTOP-16  

; BASE ADDRESS (MSB)
PMBASE=PGMMEM

; STARTING ADDRESS OF P/M GRAPHICS
PGMMEM==*256
 
; P/M DOUBLE RESOLUTION
SDMCTL=46

; PLAYER 0 NORMAL SIZE
SIZEP0=0

; CLEAR SPACE FOR PLAYER 0
ZERO(PGMMEM+512,128)  

; COPY IMAGE DATA TO P/M MEMORY
MOVEBLOCK(PGMMEM+512+60,P0,14)

; TURN ON P/M GRAPHICS
GRACTL=3

PCOLR0=50   ; COLOR OF PLAYER 0
HPOSP0=120  ; HORIZONTAL POSITION OF PLAYER 0

key=GETD(7)

; TURN OFF P/M GRAPHICS
GRACTL=0
 
RETURN
